buildscript {
    repositories {
        gradlePluginPortal()

        mavenCentral()
        mavenLocal()

    }
}

plugins {
    id 'java-library'
}

allprojects {
    group = 'fr.github.ibramsou'
    version = '1.3.2'
}

subprojects { p ->
    apply plugin: 'java'
    apply plugin: 'java-library'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    compileJava.options.encoding = 'UTF-8'

    jar {
        setArchiveBaseName('dynamic-yaml-configuration-' + (p.name as String))
    }

    properties {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    if (p.name != 'test') {
        apply plugin: 'maven-publish'
        apply plugin: 'signing'

        java {
            withJavadocJar()
            withSourcesJar()
        }

        javadocJar {
            setArchiveBaseName(jar.getArchiveBaseName().get())
        }

        sourcesJar {
            setArchiveBaseName(jar.getArchiveBaseName().get())
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    artifactId = 'dynamic-yaml-configuration-' + p.name
                    from components.java

                    pom {
                        name = 'DynamicYamlConfiguration'
                        description = 'Dynamic Yaml Configuration is an open-source Java Library used to create dynamic yaml configurations with Java annotations'
                        url = 'https://github.com/Ibramsou/YamlConfiguration'
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'Bramsou'
                                name = 'Ibrahim'
                                email = 'braimsou@gmail.com'
                            }
                        }
                        scm {
                            connection = 'scm:git:git:github.com/Ibramsou/YamlConfiguration.git'
                            developerConnection = 'scm:git:ssh://github.com:Ibramsou/YamlConfiguration.git'
                            url = 'https://github.com/Ibramsou/YamlConfiguration'
                        }
                    }
                }
            }
            repositories {
                maven {
                    name = "OSSRH"
                    url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                    credentials {
                        username sonatypeUsername
                        password sonatypePassword
                    }
                }
            }
        }

        signing {
            sign publishing.publications.mavenJava
        }
    }
}